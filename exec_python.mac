/*
 * Pythonスクリプトを実行する秀丸マクロ
 *
 * Ver.1.0 (2014-02-11)
 *
 * 機能
 * - このマクロを実行すると、編集中の内容が Python スクリプトとして実行されます。
 * - マクロの先頭で、使用する Python インタプリタとコマンドラインオプションを指定できます。
 * - 出力先を以下から選択できます。
 *   - アウトプット枠
 *   - 新しいウィンドウ
 * - python.exeへのパスが通っている必要があります。
 * - 事前コンパイルを行う場合は、#compileに1を設定してください。
 *   実行しないでコンパイルが通るかチェックします。
 *
 * yuuichi_s
 */

// Pythonインタプリタ
$python = "python";

// コマンドラインオプション
$opt = "";

// 出力先
// 0: コマンドプロンプト
// 1: アウトプット枠
// 2: 新しいウィンドウ
#output = 1;

// 実行分類
// 0: 実行
// 1: コンパイル
#compile = 0;

if (#compile == 1) {
	$opt = "-m py_compile";
}

// (無題)の場合にもそのまま実行できるようにする
if (filetype == "new") {
	// 編集内容を取得
	disabledraw;
		call SAVE_CURSOR;

		gofileend;
		$current_contents = gettext(0, 0, x, y);
		#encode = charset;

		call RESTORE_CURSOR;
	enabledraw screentopy;

	// ステルスモードのウィンドウを利用し、編集内容をファイルに保存
	#hwnd_orig = hidemaruhandle(0);
	openfile "/h";
	#hwnd_work = hidemaruhandle(0);

	// 現在の編集内容を保存するテンポラリーファイル
	$temp_file = getenv("TEMP") + "\\" + "current_contents.py";

	setencode #encode;
	insert $current_contents;
	saveas $temp_file;

	setactivehidemaru #hwnd_orig;	// 元のウィンドウに戻る
	closehidemaruforced #hwnd_work;	// 作業ウィンドウを閉じる

	call EXEC, $temp_file;
} else if (leftstr(filetype, 1) == ".") {
	// 現在のタブが「grep結果」や「実行結果」でなければ、上書き保存してから実行
	save;
	call EXEC, "%f";
}

endmacro;

// カーソル位置と範囲選択の状態をグローバル変数 #_cursor_info に保存する。
SAVE_CURSOR:
	#_cursor_info[0] = selecting;
	#_cursor_info[1] = rectselecting;
	#_cursor_info[2] = selopenx;
	#_cursor_info[3] = selopeny;
	#_cursor_info[4] = x;
	#_cursor_info[5] = y;
return;

// #_cursor_info の値から、カーソル位置と範囲選択の状態を復元する。
RESTORE_CURSOR:
	##selecting = #_cursor_info[0];
	##rectselecting = #_cursor_info[1];
	##selopenx = #_cursor_info[2];
	##selopeny = #_cursor_info[3];
	##x = #_cursor_info[4];
	##y = #_cursor_info[5];

	if (##selecting) {
		escape;
		moveto ##selopenx, ##selopeny;
		if (##rectselecting) beginrect; else beginsel;
			moveto ##x, ##y;
		endsel;
	} else {
		moveto ##x, ##y;
	}
return;

EXEC:
	$$cmd = $python;
	##stdout = 4;
	if (#output == 1) {
		##stdout = 7;
	}

	runex "\"" + $$cmd + "\" " + $opt + " \"" + $$1 + "\"",
		1,			  //sync    0:async 1:sync
		0, "",        //stdin   0:none 1:auto 2:file 3:(reserve) 4:all 5:select
		##stdout, "", //stdout  0:none 1:auto 2:file 3:add file  4:new 5:insert 6:replace 7:output frame
		1, "",        //stderr  0:none 1:=out 2:file 3:add file  4:new 5:insert 6:replace 7:output frame
		1, "",        //folder  0:none 1:current 2:specify 3:(reserve) 4:exe's folder
		2,            //show    0:auto 1:show 2:hide
		0,            //nodraw  0:draw 1:no draw
		0;            //unicode 0:ansi 2:unicode
return;
